# Copyright 2026 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This `BUILD.gn` is a manual translation of Crubit's Blaze / Bazel build files
# from Crubit.
#
# TODO(b/480138003): Automate some aspects of the translation if possible.

import("//build/rust/rust_static_library.gni")

_crubit_src_dir = "//third_party/rust-toolchain/lib/crubit"

# Following go/build-best-practice#visibility ("Visibility needs to be scoped as tightly as
# possible") by making default visibility private, and forcing public targets to explicitly
# set their `visibility`.
visibility = [ ":*" ]

# The list of targets below tries to replicate `_cc_deps_for_bindings`
# from `crubit/cc_bindings_from_rs/bazel_crubit/support/cc_bindings_from_rust_rule.bzl`
# These are the dependencies of the `.h` file generated by `cpp_api_from_rust`.
group("cpp_api_from_rust_bindings_cpp_deps") {
  # This `group` is public because a `rust_static_library.cpp_api_from_rust` can be used from
  # any directory, but there are no scenarios where end-users need to depend on this `group`.
  # (In practice this `group` will be injected into `deps` by `cpp_api_from_rust` template.)
  visibility = []
  visibility = [ "*" ]

  public_deps = [
    ":rs_std",
    ":support_all_annotations",
    ":support_bridge_cpp",
    ":support_internal_bindings_support",
  ]
}

# The list of targets below tries to replicate `_rs_deps_for_bindings`
# from `crubit/cc_bindings_from_rs/bazel_crubit/support/cc_bindings_from_rust_rule.bzl`
# These are the dependencies of the `.rs` file generated by `cpp_api_from_rust`.
group("cpp_api_from_rust_bindings_rs_deps") {
  # This `group` is public because a `rust_static_library.cpp_api_from_rust` can be used from
  # any directory, but there are no scenarios where end-users need to depend on this `group`.
  # (In practice this `group` will be injected into `deps` by `cpp_api_from_rust` template.)
  visibility = []
  visibility = [ "*" ]

  public_deps = [ ":support_bridge_rust" ]
}

config("config") {
  # TODO(b/474627465): Once the linked bug is resolved, only one of these
  # include paths should be needed.
  include_dirs = [
    "${_crubit_src_dir}",
    "${_crubit_src_dir}/..",
  ]
}

# Equivalent of Bazel's `//support/internal:bindings_support`.
source_set("support_internal_bindings_support") {
  public_configs = [ ":config" ]
  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]

  sources = [
    "${_crubit_src_dir}/support/internal/check_no_mutable_aliasing.cc",
    "${_crubit_src_dir}/support/internal/check_no_mutable_aliasing.h",
    "${_crubit_src_dir}/support/internal/cxx20_backports.h",
    "${_crubit_src_dir}/support/internal/lazy_init.h",
    "${_crubit_src_dir}/support/internal/memswap.h",
    "${_crubit_src_dir}/support/internal/offsetof.h",
    "${_crubit_src_dir}/support/internal/sizeof.h",
    "${_crubit_src_dir}/support/internal/slot.h",
  ]
  deps = [ "//third_party/abseil-cpp:absl" ]
}

# Equivalent of Bazel's:
#
# * `//support:annotations`
# * `//support:lifetime_annotations`
source_set("support_all_annotations") {
  public_configs = [ ":config" ]
  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]

  public = [
    "${_crubit_src_dir}/support/annotations.h",
    "${_crubit_src_dir}/support/lifetime_annotations.h",
  ]
}

# Equivalent of Bazel's:
#
# * `//support/rs_std:char`.
# * `//support/rs_std:slice_ref`.
# * `//support/rs_std:str_ref`.
# * `//support/rs_std/internal:is_utf8`.
source_set("rs_std") {
  # This is a public `source_set` - any target can depend on it.
  visibility = []
  visibility = [ "*" ]

  public_configs = [ ":config" ]
  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]

  public = [
    "${_crubit_src_dir}/support/rs_std/char.h",
    "${_crubit_src_dir}/support/rs_std/internal/is_utf8.h",
    "${_crubit_src_dir}/support/rs_std/slice_ref.h",
    "${_crubit_src_dir}/support/rs_std/str_ref.h",
  ]
  sources = [ "${_crubit_src_dir}/support/rs_std/char.cc" ]
  deps = [
    ":support_all_annotations",
    ":support_internal_bindings_support",
    "//third_party/abseil-cpp:absl",
  ]
}

# Equivalent of Bazel's `//support:bridge_cpp`.
source_set("support_bridge_cpp") {
  public_configs = [ ":config" ]
  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [ "//build/config/compiler:no_chromium_code" ]

  public = [ "${_crubit_src_dir}/support/bridge.h" ]
}

# Equivalent of Bazel's `//support:bridge_rust`.
rust_static_library("support_bridge_rust") {
  # Opt out of crate name mangling.
  # TODO(lukasza): Asking about this in an internal chat here:
  # https://chat.google.com/room/AAAAImO--WA/byX8BcXq9lU/byX8BcXq9lU?cls=10
  crate_name = "bridge_rust"
  crate_root = "${_crubit_src_dir}/support/bridge.rs"
  allow_unsafe = true
  sources = [ crate_root ]
}
